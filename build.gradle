plugins {
  id 'java-library'
  id 'eclipse'
  id 'idea'
  id 'maven-publish'
  id 'net.neoforged.gradle.userdev' version '7.0.165'
  id "me.shedaniel.unified-publishing" version "0.1.+"
  id "io.freefair.lombok" version "8.6"
}

tasks.named('wrapper', Wrapper).configure {
  distributionType = Wrapper.DistributionType.BIN
}

repositories {
  mavenLocal()
}

base {
  archivesName = archives_base_name
  version = "${minecraft_version}-${mod_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
java.withSourcesJar()

minecraft {
  accessTransformers {
    file("src/main/resources/META-INF/accesstransformer.cfg")
  }
}

runs {
  configureEach {
    systemProperty 'forge.logging.markers', 'REGISTRIES'
    systemProperty 'forge.logging.console.level', 'debug'

    modSource project.sourceSets.main
  }

  client {
    systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
  }

  server {
    systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    programArgument '--nogui'
  }

  gameTestServer {
    systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
  }

  data {
    arguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
  }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
  runtimeClasspath.extendsFrom localRuntime
}

repositories {
  maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
  maven {
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
    content {
      includeGroup "mezz.jei"
      includeGroup "com.hollingsworth.ars_nouveau"
      includeGroup "vazkii.patchouli"
    }
  }
  maven {
    name = "Shedaniel's maven"
    url = "https://maven.shedaniel.me/"
    content {
      includeGroup "me.shedaniel.cloth"
    }
  }
  maven {
    name = "Lat's maven"
    url = "https://maven.saps.dev/releases"
    content {
      includeGroup "dev.latvian.mods"
      includeGroup "dev.latvian.apps"
    }
  }
  maven {
    url = "https://maven.architectury.dev"
    content {
      includeGroup "dev.architectury"
    }
  }
  maven {
    name = "Mod maven"
    url = "https://modmaven.dev/"
    content {
      includeGroup "mcjty.theoneprobe"
      includeGroup "mekanism"
      includeGroup 'appeng'
    }
  }
  maven {
    url "https://www.cursemaven.com"
    content {
      includeGroup "curse.maven"
    }
  }
  maven {
    name = "Covers"
    // for rf stuffs
    url = "https://maven.covers1624.net"
  }
  //Needed for KubeJS
  maven {
    url 'https://jitpack.io'
    content {
      includeGroup "com.github.rtyley"
    }
  }
  maven { url = "https://maven.firstdarkdev.xyz/snapshots" } // LDLib
  maven {
    name = "TerraformersMC"
    url = "https://maven.terraformersmc.com/"
    content {
      includeGroup "dev.emi"
    }
  }
  maven {
    name = "Illusive Soulworks maven"
    url = "https://maven.theillusivec4.top/"
  }
  maven {
    name = "OctoStudios"
    url = uri("https://maven.octo-studios.com/releases")
  }
  maven {
    name = 'GeckoLib'
    url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
    content {
      includeGroup("software.bernie.geckolib")
    }
  }
  mavenCentral()
  mavenLocal()
}

dependencies {
  // lombok
  compileOnly "org.projectlombok:lombok:${project.lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
  implementation "net.neoforged:neoforge:${neo_version}"
  compileOnly localRuntime("es.degrassi:modular_machinery_reborn:${minecraft_version}-${mmr_version}")
  compileOnly localRuntime("es.degrassi:modular_machinery_reborn_mekanism:${minecraft_version}-${mmr_mekanism_version}")
  compileOnly localRuntime("es.degrassi:modular_machinery_reborn_ars:${minecraft_version}-${mmr_ars_version}")

  // ProbeJS
  localRuntime("curse.maven:probejs-585406:5820894")

  // JEI
  compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
  compileOnly localRuntime("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")

  // Applied Energistics
  localRuntime compileOnly("curse.maven:applied-energistics-2-223794:${ae2_version}")
  localRuntime compileOnly("curse.maven:applied-mekanistics-574300:${ae2_mekanism_version}")
  localRuntime compileOnly("curse.maven:ars-energistique-905641:${ae2_ars_version}")

  // Mekanism Stuff
  compileOnly("mekanism:Mekanism:${mekanism_version}:api")
  runtimeOnly("mekanism:Mekanism:${mekanism_version}:all")

  // Ars Stuff
  localRuntime ("software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}")
  localRuntime ("vazkii.patchouli:Patchouli:1.21-${patchouli_version}")
  localRuntime "top.theillusivec4.curios:curios-neoforge:${curios_version}"
  localRuntime compileOnly ("com.hollingsworth.ars_nouveau:ars_nouveau-${minecraft_version}:${ars_version}.841")

  // KubeJS
  localRuntime compileOnly("dev.latvian.mods:kubejs-neoforge:${kubejs_version}")

  // Jade
  localRuntime compileOnly("curse.maven:jade-324717:${jade_version}")
}

tasks.withType(ProcessResources).configureEach {
  var replaceProperties = [
      minecraft_version      : minecraft_version,
      minecraft_version_range: minecraft_version_range,
      neo_version            : neo_version,
      neo_version_range      : neo_version_range,
      loader_version_range   : loader_version_range,
      mod_id                 : mod_id,
      mod_name               : mod_name,
      mod_license            : mod_license,
      mod_version            : mod_version,
      mod_authors            : mod_authors,
      mod_description        : mod_description,
      mod_url                : mod_url,
      icon                   : icon,
      pack_format_number     : pack_format_number,
      mekanism_version       : mekanism_version,
      mmr_version            : mmr_version,
      ae2_version            : ae2_version_string
  ]
  inputs.properties replaceProperties

  filesMatching(['META-INF/neoforge.mods.toml']) {
    expand replaceProperties
  }
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
  publications {
    register('mavenJava', MavenPublication) {
      groupId = maven_group
      artifactId = archivesBaseName
      version = project.version
      from components.java

      pom {
        name = mod_name
        description = mod_description
        packaging = 'jar'
        scm {
          url = mod_github
        }
        issueManagement {
          system = 'github'
          url = mod_issues
        }
        licenses {
          license {
            name = mod_license
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = mod_authors
            name = mod_authors
            roles = ['developper']
          }
        }
      }
    }
  }
  repositories {
    maven {
      name = 'maven'
      url = 'D:\\local_maven'
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
  module {
    downloadSources = true
    downloadJavadoc = true
  }
}

unifiedPublishing {
  project {
    displayName = "[NeoForge]ModularMachineyRebornEnergistics-${minecraft_version}-${mod_version}"
    releaseType = "release"
    if(rootProject.file("CHANGELOG.md").exists()) {
      changelog = rootProject.file("CHANGELOG.md").text
    }
    gameVersions = ["${minecraft_version}", "1.21"]
    gameLoaders = ["neoforge"]
    mainPublication jar

    var CURSE_API_KEY = System.getenv("CURSEFORGE")
    if (CURSE_API_KEY != null) {
        curseforge {
            token = CURSE_API_KEY
            id = "1145146"
            gameVersions.addAll "Java 21"
            relations {
                depends "modular-machinery-reborn"
                depends "applied-energistics-2"
                optional "modular-machinery-reborn-mekanism"
                optional "modular-machinery-reborn-ars-nouveau"
                optional "applied-mekanistics"
                optional "ars-energistique"
                optional "ars-nouveau"
                optional "mekanism"
                optional "kubejs"
                optional "jei"
                optional "emi"
                optional "jade"
            }
        }
    }

    var MODRINTH_API_KEY = System.getenv("MODRINTH")
    if (MODRINTH_API_KEY != null) {
      modrinth {
        token = MODRINTH_API_KEY
        id = "XQWiHmew"
        relations {
          depends "ae2"
          depends "modular-machinery-reborn"
          optional "modular-machinery-reborn-mekanism"
          optional "modular-machinery-reborn-ars-nouveau"
          optional "applied-mekanistics"
          optional "ars-energistique"
          optional "ars-nouveau"
          optional "mekanism"
          optional "kubejs"
          optional "jei"
          optional "emi"
          optional "jade"
        }
      }
    }
  }
}
